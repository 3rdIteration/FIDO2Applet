buildscript {
    repositories {
        mavenCentral()
        maven { url "https://javacard.pro/maven" }
    }
}

plugins {
    id("java")
    id("com.klinec.gradle.javacard") version "1.8.0"
}

group = "us.q3q"
version = "1.0-SNAPSHOT"

repositories {
    mavenCentral()
}

dependencies {
    testImplementation(group: 'com.klinec', name: 'jcardsim', version: '3.0.5.11') {
        // Javacard will be provided by the user at runtime through the JC_HOME env var
        exclude(group: 'oracle.javacard', module: 'api_classic')
    }
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.8.1'

    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.8.1'
}

test {
    useJUnitPlatform()
}

tasks.register('testJar', Jar) {
    archiveBaseName = project.name + '-tests'
    duplicatesStrategy = 'include'
    from sourceSets.test.output + sourceSets.test.allSource
    from {
        sourceSets.test.runtimeClasspath.filter {
            it.toString().indexOf("jcardsim-") != -1
        }.collect {
            zipTree(it)
        }
    }
}

javacard {
    config {
        cap {
            packageName 'us.q3q.fido2'
            version '0.1'
            aid 'A0:00:00:06:47'
            output 'FIDO2.cap'
            applet {
                className 'us.q3q.fido2.FIDO2Applet'
                aid 'A0:00:00:06:47:2F:00:01'
            }
        }
    }
}
